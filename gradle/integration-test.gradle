/* Required plugins:
*  * idea
*  * com.adarshr.test-logger
*/

sourceSets {
	integrationTest {
		compileClasspath += sourceSets.main.output + sourceSets.test.output
		runtimeClasspath += sourceSets.main.output + sourceSets.test.output
	}
}

idea {
	module {
		testSourceDirs += sourceSets.integrationTest.java.srcDirs
		testResourceDirs += sourceSets.integrationTest.resources.srcDirs
		scopes.TEST.plus += [configurations.integrationTestCompile]
	}
}

configurations {
	integrationTestImplementation.extendsFrom testImplementation
	integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

task integrationTest(type: Test) {
	description = 'Runs integration tests.'
	group = 'verification'

	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
	shouldRunAfter test
}

integrationTest {
	useJUnitPlatform()
}

check.dependsOn integrationTest

testlogger {
	theme 'mocha' // project level
}
